# Load Balancer Module for Azure

A Terraform module for deploying a Load Balancer for VM-Series firewalls.
Supports both standalone and scale set deployments.

> [!NOTE]
> The module can create a public or a private Load Balancer
> Due to that some properties are mutually exclusive.

The module creates a single Load Balancer and a single backend for it, but it allows multiple frontends.

> [!NOTE]
> In case of a public Load Balancer, you can define outbound rules and use the frontend's public IP address to access the internet.
> If this approach is chosen please note that all inbound rules will have the outbound SNAT disabled as you cannot mix
> SNAT with outbound rules for a single backend.

## Usage

There are two basic modes the module can work in: a public and a private Load Balancer.

### Private Load Balancer

To create a private Load Balancer one has to specify an ID of an existing Subnet and a private IP address
in each frontend IP configuration.

Example of a private Load Balancer with HA ports rule:

```hcl
module "lbi" {
  source = "../../modules/loadbalancer"

  name                = "private-lb"
  location            = "West Europe"
  resource_group_name = "existing-rg"

  frontend_ips = {
    ha = {
      name               = "HA"
      subnet_id          = "/subscription/xxxx/......."
      private_ip_address = "10.0.0.1"
      in_rules = {
        ha = {
          name     = "HA"
          port     = 0
          protocol = "All"
        }
      }
    }
  }
}
```

### Public Load Balancer

To create a public Load Balancer one has to specify a name of a public IP resource (existing or new)
in each frontend IP configuration.

Example of a private Load Balancer with a single rule for port `80`:

```hcl
module "lbe" {
  source = "../../modules/loadbalancer"

  name                = "public-lb"
  location            = "West Europe"
  resource_group_name = "existing-rg"

  frontend_ips = {
    web = {
      name             = "web-traffic"
      public_ip_name   = "public-ip"
      create_public_ip = true
      in_rules = {
        http = {
          name     = "http"
          port     = 80
          protocol = "TCP"
        }
      }
    }
  }
}
```
