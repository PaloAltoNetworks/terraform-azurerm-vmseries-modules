# Palo Alto Networks VNet Module for Azure

A Terraform module for deploying a Virtual Network and its components required for the VM-Series firewalls in Azure.

## Usage

This module is designed to work in several *modes* depending on which variables or flags are set. Most common usage scenarios are:

- create all -  creates a VNET, Subnet, NSGs and Route Tables. In this example the two latter are assigned to the Subnet. The NSG and Route Table have rules defined:
  
  ```hcl
  name                = "transit"
  resource_group_name = "existing-rg"
  address_space       = ["10.0.0.0/25"]
  network_security_groups = {
    inbound = {
      name = "inbound-nsg"
      rules = {
        mgmt_inbound = {
          name                       = "allow-traffic"
          priority                   = 100
          direction                  = "Inbound"
          access                     = "Allow"
          protocol                   = "Tcp"
          source_address_prefixes    = ["1.2.3.4"]
          source_port_range          = "*"
          destination_address_prefix = "10.0.0.0/28"
          destination_port_ranges    = ["22", "443"]
        }
      }
    }
  }
  route_tables = {
    default = {
      name = "default-rt"
      routes = {
        "default" = {
          name                   = "default-udr"
          address_prefix         = "0.0.0.0/0"
          next_hop_type          = "VirtualAppliance"
          next_hop_in_ip_address = "5.6.7.8"
        }
      }
    }
  }
  subnets = {
    "subnet" = {
      name                   = "snet"
      address_prefixes       = ["10.0.0.0/28"]
      network_security_group = "inbound"
      route_table            = "default"
    }
  }
  ```

- source a VNET but create Subnets, NSGs and Route Tables. This is a similar example to the above one, NSG and Route Table are empty this time:

  ```hcl
  create_virtual_network = false
  name                   = "existing-vnet"
  resource_group_name    = "existing-rg"
  network_security_groups = {
    inbound = { name = "inbound-nsg" }
  }
  route_tables = {
    default = { name = "default-rt" }
  }
  subnets = {
    "subnet" = {
      name                   = "snet"
      address_prefixes       = ["10.0.0.0/28"]
      network_security_group = "inbound"
      route_table            = "default"
    }
  }
  ```

- source a VNET and Subnet, but create NSGs and Route Tables. This is a common brownfield use case: we will source Subnets, and create and assign NSGs and Route Tables to them:

  ```hcl
  create_virtual_network = false
  name                   = "existing-vnet"
  resource_group_name    = "existing-rg"
  network_security_groups = {
    inbound = {
      name = "inbound-nsg"
      rules = {
        mgmt_inbound = {
          name                       = "allow-traffic"
          priority                   = 100
          direction                  = "Inbound"
          access                     = "Allow"
          protocol                   = "Tcp"
          source_address_prefixes    = ["1.2.3.4"]
          source_port_range          = "*"
          destination_address_prefix = "10.0.0.0/28"
          destination_port_ranges    = ["22", "443"]
        }
      }
    }
  }
  route_tables = {
    default = {
      name = "default-rt"
      routes = {
        "default" = {
          name                   = "default-udr"
          address_prefix         = "0.0.0.0/0"
          next_hop_type          = "VirtualAppliance"
          next_hop_in_ip_address = "5.6.7.8"
        }
      }
    }
  }
  create_subnets = false
  subnets = {
    "subnet" = {
      name                   = "snet"
      network_security_group = "inbound"
      route_table            = "default"
    }
  }
  ```
