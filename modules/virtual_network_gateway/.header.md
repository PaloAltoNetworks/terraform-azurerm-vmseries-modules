# Palo Alto Networks Virtual Network Gateway Module for Azure

A terraform module for deploying a VNG (Virtual Network Gateway) and its components required for the VM-Series firewalls in Azure.

## Usage

In order to use module `virtual_network_gateway`, you need to deploy `azurerm_resource_group` and use module `vnet` as prerequisites. 
Then you can use below code as an example of calling module to create VNG:

```hcl
module "vng" {
  source = "../../modules/virtual_network_gateway"

  for_each = var.virtual_network_gateways

  location            = var.location
  resource_group_name = local.resource_group.name
  name                = each.value.name
  zones               = each.value.avzones

  type     = each.value.type
  vpn_type = each.value.vpn_type
  sku      = each.value.sku

  active_active                    = each.value.active_active
  default_local_network_gateway_id = each.value.default_local_network_gateway_id
  edge_zone                        = each.value.edge_zone
  enable_bgp                       = each.value.enable_bgp
  generation                       = each.value.generation
  private_ip_address_enabled       = each.value.private_ip_address_enabled

  ip_configuration = [
    for ip_configuration in each.value.ip_configuration :
    merge(ip_configuration, { subnet_id = module.vnet[ip_configuration.vnet_key].subnet_ids[ip_configuration.subnet_name] })
  ]

  vpn_client_configuration  = each.value.vpn_client_configuration
  azure_bgp_peers_addresses = each.value.azure_bgp_peers_addresses
  local_bgp_settings        = each.value.local_bgp_settings
  custom_route              = each.value.custom_route
  ipsec_shared_key          = each.value.ipsec_shared_key
  local_network_gateways    = each.value.local_network_gateways
  connection_mode           = each.value.connection_mode
  ipsec_policy              = each.value.ipsec_policy

  tags = var.tags
}
```

Below there are provided sample values for `virtual_network_gateways` map:

```hcl
virtual_network_gateways = {
  "vng" = {
    name          = "vng"
    type          = "Vpn"
    sku           = "VpnGw2"
    generation    = "Generation2"
    active_active = true
    enable_bgp    = true
    ip_configuration = [
      {
        name             = "001"
        create_public_ip = true
        public_ip_name   = "pip1"
        vnet_key         = "transit"
        subnet_name      = "GatewaySubnet"
      },
      {
        name             = "002"
        create_public_ip = true
        public_ip_name   = "pip2"
        vnet_key         = "transit"
        subnet_name      = "GatewaySubnet"
      }
    ]
    ipsec_shared_key = "test123"
    azure_bgp_peers_addresses = {
      primary_1   = "169.254.21.2"
      secondary_1 = "169.254.22.2"
    }
    local_bgp_settings = {
      asn = "65002"
      peering_addresses = {
        "001" = {
          apipa_addresses = ["primary_1"]
        },
        "002" = {
          apipa_addresses = ["secondary_1"]
        }
      }
    }
    local_network_gateways = {
      "lg1" = {
        local_ng_name   = "lg1"
        connection_name = "cn1"
        gateway_address = "8.8.8.8"
        remote_bgp_settings = [{
          asn                 = "65000"
          bgp_peering_address = "169.254.21.1"
        }]
        custom_bgp_addresses = [
          {
            primary   = "primary_1"
            secondary = "secondary_1"
          }
        ]
      },
      "lg2" = {
        local_ng_name   = "lg2"
        connection_name = "cn2"
        gateway_address = "4.4.4.4"
        remote_bgp_settings = [{
          asn                 = "65000"
          bgp_peering_address = "169.254.22.1"
        }]
        custom_bgp_addresses = [
          {
            primary   = "primary_1"
            secondary = "secondary_1"
          }
        ]
      }
    }
    connection_mode = "InitiatorOnly"
    ipsec_policy = [
      {
        dh_group         = "ECP384"
        ike_encryption   = "AES256"
        ike_integrity    = "SHA256"
        ipsec_encryption = "AES256"
        ipsec_integrity  = "SHA256"
        pfs_group        = "ECP384"
        sa_datasize      = "102400000"
        sa_lifetime      = "14400"
      }
    ]
  }
}
```